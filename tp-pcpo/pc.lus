include "memoire.lus"

type state = struct { char, b1, b1a, b1b, b2, b2a, b2b, b2c, b2if, b2ifa, b2ifb, done : bool };

node pc (reset, On, C, Z : bool)
returns (
    uc0, uc1, lsl, var1, var2, var3, chQ, chR, chA, chB, chK : bool;
    stateC : state; -- état courant
);
var stateN : state; -- état suivant
let
    -- stockage de l'état
    stateC.char = bascule(stateN.char, true, false, reset);
    stateC.b1 = bascule(stateN.b1, true, reset, false);
    stateC.b1a = bascule(stateN.b1a, true, reset, false);
    stateC.b1b = bascule(stateN.b1b, true, reset, false);
    stateC.b2 = bascule(stateN.b2, true, reset, false);
    stateC.b2a = bascule(stateN.b2a, true, reset, false);
    stateC.b2b = bascule(stateN.b2b, true, reset, false);
    stateC.b2c = bascule(stateN.b2c, true, reset, false);
    stateC.b2if = bascule(stateN.b2if, true, reset, false);
    stateC.b2ifa = bascule(stateN.b2ifa, true, reset, false);
    stateC.b2ifb = bascule(stateN.b2ifb, true, reset, false);
    stateC.done = bascule(stateN.done, true, reset, false);

    -- calcul de l'état suivant
    stateN.char = (stateC.char and not On);
    stateN.b1 = (stateC.char and On) or (stateC.b1b);
    stateN.b1a = (stateC.b1 and (not C or Z));
    stateN.b1b = (stateC.b1a);
    stateN.b2 = (stateC.b1 and (C and not Z)) or (stateC.b2if and not C) or (stateC.b2ifb);
    stateN.b2a = (stateC.b2 and (C and not Z));
    stateN.b2b = (stateC.b2a);
    stateN.b2c = (stateC.b2b);
    stateN.b2if = (stateC.b2c);
    stateN.b2ifa = (stateC.b2if and C);
    stateN.b2ifb = (stateC.b2ifa);
    stateN.done = (stateC.b2 and (not C or Z)) or (stateC.done);

    -- calcul des sorties
    uc0, uc1, lsl, var1, var2, var3, chQ, chR, chA, chB, chK = pc_sorties(stateC);
tel;

function pc_sorties (s : state) returns (uc0, uc1, lsl, var1, var2, var3, chQ, chR, chA, chB, chK : bool);
let
    uc0 = s.b1a or s.b2 or s.b2a or s.b2c or s.b2b;
    uc1 = s.b1a or s.b1b or s.b2 or s.b2a or s.b2ifa or s.b2c;
    lsl = s.b1a or s.b2c;
    var1 = s.b1 or s.b1a or s.b1b or s.b2 or s.b2a or s.b2b;
    var2 = s.b1b or s.b2 or s.b2b or s.b2if or s.b2ifb;
    var3 = s.b1;
    chQ = s.b2c or s.b2ifa;
    chR = s.b2ifb;
    chA = false;
    chB = s.b1a or s.b2a;
    chK = s.b1b or s.b2b;
tel;
